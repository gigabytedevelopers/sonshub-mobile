apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.firebase.crashlytics'

// Load keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "SonsHub-${variant.name}-${variant.versionName}.apk"
        }
    }

    defaultConfig {
        applicationId "com.gigabytedevelopersinc.apps.sonshub"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 14
        versionName "1.1.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
		signingConfig signingConfigs.release
            minifyEnabled false
            manifestPlaceholders = [appNameSuffix: ""]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-debug"
            minifyEnabled false
            applicationIdSuffix '.dev'
            manifestPlaceholders = [appNameSuffix: " Dev"]
            debuggable true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    api 'com.google.android.material:material:1.2.0-beta01'
    implementation 'com.android.volley:volley:1.1.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }
    implementation "androidx.browser:browser:1.2.0"
    implementation 'com.google.firebase:firebase-core:17.4.2'
    implementation 'com.google.firebase:firebase-messaging:20.2.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'jp.wasabeef:glide-transformations:3.1.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.ogaclejapan.smarttablayout:library:2.0.0@aar'
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:2.0.0@aar'
    implementation 'com.github.recruit-lifestyle:WaveSwipeRefreshLayout:1.6'
    implementation 'com.squareup.okhttp3:okhttp:4.6.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.9.4'
    implementation 'com.google.android.exoplayer:exoplayer-dash:2.9.4'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.9.4'
    implementation 'com.google.android.exoplayer:exoplayer-smoothstreaming:2.9.4'
    implementation 'com.google.android.exoplayer:exoplayer-hls:2.9.4'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "com.github.firdausmaulan:GlideSlider:$glide_slider_version"
    implementation "com.leinardi.android:speed-dial:3.1.1"
    implementation 'saschpe.android:customtabs:2.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // App Update
    implementation 'com.github.javiersantos:AppUpdater:2.7'

    // Downloader Fetcher
    implementation project(':fetch2')
    implementation project(':fetch2okhttp')
    implementation project(':fetch2rx')

    // Music Player Dependencies
    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // AndroidX
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.core:core-ktx:1.4.0-alpha01'

    // Arch Components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'

    // Other Google
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.mediarouter:mediarouter:1.1.0'
    implementation 'com.google.android.gms:play-services-cast-framework:18.1.0'

    // Third Party
    implementation 'com.afollestad:rxkprefs:1.2.4'
    implementation 'com.afollestad.material-dialogs:core:2.0.0'
    implementation 'com.afollestad.material-dialogs:input:2.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'org.koin:koin-android:2.0.1'
    implementation 'org.koin:koin-androidx-scope:2.0.1'
    implementation 'org.koin:koin-androidx-viewmodel:2.0.1'

    // Testing/Debug
    implementation 'com.jakewharton.timber:timber:4.7.1'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.3'
}

configurations {
    implementation.exclude group: "com.android.support", module: "customtabs"
    implementation.exclude group: "com.google.firebase", module: "firebase-measurement-connector-impl"
}
apply plugin: 'com.google.gms.google-services'